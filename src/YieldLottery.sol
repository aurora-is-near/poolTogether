// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import "openzeppelin-contracts/token/ERC20/IERC20.sol";
import "./interfaces/IJetStaking.sol";

/**
 * @title Aurora Yield Lottery
 * @author Aurora Team
 * 
 * @notice This contract allows users to pool together their aurora for a specified 
 *         period of time, afterwhich a user is selected at random as the "winner".
 *         The "winner" receives the combined yield generated by all users + initial 
 *         stake provided. All users are refunded their initial investment.
 * 
 * @dev Two key problems to solve for this are:
 *      - Mechanism to decide winner (one address one ticket, or one aurora one ticket, or something else?)
 *      - Where to get source of randomness - the EVM is inherently deterministic, hence its virtually impossible 
 *        to achieve this *within* the blockchain. Blockhash, blockTimestamp, nonce etc. can be gamed easily.
 * 
 *     Solution: The selection of the winner will be outsourced to a trusted admin. Said admin will receive a list 
 *               (via emitted events) of user addresses and their corresponding aurora staked, and will generate a winner.
 *               Would recommend to use one aurora one ticket - otherwise incentivises users to spread aurora across multiple wallets
 */

contract YieldLottery {
    // Window of time within which users can deposit aurora
    uint256 public openWindow;
    // Admin of the contract
    address public admin;
    // Allows control over deposits
    bool public paused;
    // Aurora token
    IERC20 public aurora;
    // Stream tokens
    IERC20[] public streamTokens;
    // JetStaking
    IJetStaking public jetStaking;
    // Array of epochs
    Epoch[] public epochs;
    // Mapping of epochId => address => Position
    mapping(uint256 => mapping(address => Position)) public userTickets;

    enum Status {
        Active,
        Ended
    }

    // Position represents a user's stake
    struct Position {
        uint256 amount;
        bool claimed;
    }

    // 1 Epoch == 1 "lottery game"
    struct Epoch {
        uint256 startTime;
        uint256 endTime;
        uint256 initialBal;
        uint256 finalBal;
        address winner;
        bool withdrawalOpen;
        Status status;
    }

    event Staked(address indexed user, uint256 indexed epochId, uint256 amount);

    modifier onlyAdmin() {
        require(msg.sender == admin, "ONLY_ADMIN_CAN_CALL");
        _;
    }

    modifier notPaused() {
        require(!paused, "CONTRACT_PAUSED");
        _;
    }

    constructor(address _admin) {
        admin = _admin;
    }

    // Initialization of state variables
    function init(uint256 _openWindow, address _aurora, address _jetStaking, address[] calldata _streamTokens)
        external
        onlyAdmin
    {
        openWindow = _openWindow;
        aurora = IERC20(_aurora);
        jetStaking = IJetStaking(_jetStaking);
        for (uint256 i; i < _streamTokens.length;) {
            streamTokens.push(IERC20(_streamTokens[i]));
            unchecked {
                i++;
            }
        }
        aurora.approve(address(jetStaking), type(uint256).max);
    }

    // @notice Allows users to buy tickets in the current epoch
    // @param _amount Amount of aurora tokens to stake
    // @dev Function checks that the epoch is live and within the "betting window"
    //      It then updates a mapping and stakes the user's aurora tokens
    function buyTickets(uint256 _amount) external notPaused {
        Epoch storage currentEpoch = epochs[epochs.length - 1];
        require(currentEpoch.status == Status.Active, "NO_LIVE_EPOCHS");
        require(currentEpoch.startTime + openWindow > block.timestamp, "EPOCH_CLOSED");
        aurora.transferFrom(msg.sender, address(this), _amount);
        userTickets[epochs.length - 1][msg.sender].amount += _amount;
        jetStaking.stake(_amount);
        currentEpoch.initialBal += _amount;

        emit Staked(msg.sender, epochs.length - 1, _amount);
    }

    // @notice Allows users to claim their tokens after epoch is finished
    // @param _epochId Epoch that they want to claim their tokens for
    // @dev Function makes sure said epoch: is concluded, the 2 day withdrawal wait
    //      is over. and user hasn't already claimed. It then transfers the user's
    //      initial depposit back, and checks if the user is the winner to send the
    //      yield generated.
    function claimTickets(uint256 _epochId) public {
        Epoch memory epoch = epochs[_epochId];
        require(epoch.status == Status.Ended, "EPOCH_NOT_CONCLUDED");
        require(epoch.withdrawalOpen, "WITHDRAWAL_UNAVAILABLE");
        require(!userTickets[_epochId][msg.sender].claimed, "ALREADY_CLAIMED");
        uint256 balance = userTickets[_epochId][msg.sender].amount;
        aurora.transfer(msg.sender, balance);
        if (epoch.winner == msg.sender) {
            uint256 prize = epoch.finalBal - epoch.initialBal;
            aurora.transfer(msg.sender, prize);
            for (uint256 i; i < streamTokens.length;) {
                uint256 bal = streamTokens[i].balanceOf(address(this));
                streamTokens[i].transfer(msg.sender, bal);
                unchecked {
                    i++;
                }
            }
        }
        userTickets[_epochId][msg.sender].claimed = true;
    }

    function claimLatest() external {
        claimTickets(epochs.length - 2);
    }

    // @notice Allows admin to close an epoch and decide the winner
    // @param _winner Winning address for the currentEpoch
    // @dev Function updates epoch variables and moves all rewardstoPending.
    function concludeEpoch(address _winner) external onlyAdmin {
        Epoch storage currentEpoch = epochs[epochs.length - 1];
        require(currentEpoch.status == Status.Active, "NO_LIVE_EPOCH");
        currentEpoch.endTime = block.timestamp;
        currentEpoch.winner = _winner;
        currentEpoch.status = Status.Ended;
        uint256 auroraBal = (jetStaking.getTotalAmountOfStakedAurora() * jetStaking.getUserShares(address(this)))
            / jetStaking.totalAuroraShares();
        currentEpoch.finalBal = auroraBal;
        jetStaking.moveAllRewardsToPending();
        jetStaking.unstakeAll();
    }

    // @notice Allows admin to withdraw tokens once the 2 day delay is over
    // @notice _epochId Allows function to know which epochId to update
    function withdraw(uint256 _epochId) external onlyAdmin {
        jetStaking.withdrawAll();
        epochs[_epochId].withdrawalOpen = true;
    }

    // @notice Creates a new epoch and pushes to the array
    function newEpoch() external onlyAdmin {
        epochs.push(Epoch(block.timestamp, 0, 0, 0, address(0), false, Status.Active));
    }

    /*/////////////////////////////////////////////////////
                    HELPER FUNCTIONS
    /////////////////////////////////////////////////////*/

    function setAdmin(address _admin) external onlyAdmin {
        admin = _admin;
    }

    function setOpenWindow(uint256 _time) external onlyAdmin {
        openWindow = _time;
    }

    function pause() external onlyAdmin {
        paused = true;
    }
}
